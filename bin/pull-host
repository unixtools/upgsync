#!/usr/bin/perl

# Begin-Doc
# Name: pull-host
# Description: pulls host content
# End-Doc

use strict;
use lib "/local/perllib/libs";
use lib "/local/mstperl/libs";

use Getopt::Long;
use File::Path;

my $debug;
my $help;
my $partial;
my $res = GetOptions(
    "debug+"    => \$debug,
    "partial|p" => \$partial,
    "help|h"    => \$help,
);

my $host = shift @ARGV;

if ( !$res || $help || !$host ) {
    print "Usage: $0 host [--debug] [--partial] [--help]\n";
    exit(1);
}

my ($full) = gethostbyname($host);
if ( $full && $full ne $host ) {
    print "Overriding hostname with: $full\n";
    $host = $full;
}

unlink("/root/.ssh/known_hosts");

#
# Check to see if ssh agent socket
#
if ( $ENV{SSH_AUTH_SOCK} ) {
    die "Must have SSH agent socket.\n";
}

#
# Set up base
#
my $base = "/data/hosts/$host";
if ( !-e $base ) {
    mkpath( $base, 0, 0700 ) || die;
}
if ( !-e "$base/upgsync-logs" ) {
    mkpath( $base . "/upgsync-logs", 0, 0700 ) || die;
}

#
# First determine OS of target system as previously synced
#
open( my $in, "-|" ) || exec("ssh root\@$host uname -a");
chomp( my $uname = <$in> );
close($in);

if ( -e "$base/info-uname-a.txt" ) {
    open( my $in, "$base/info-uname-a.txt" );
    chomp( my $old_uname = <$in> );
    close($in);

    if ( $uname ne $old_uname ) {
        print "\n\n";
        print "Host OS Mismatched, will not proceed:\n\n";
        print "  Old: $old_uname\n";
        print "  New: $uname\n\n";
        print "If resync is intentional:\n\n    rm -f $base/info-uname-a.txt\n\n";
        exit(1);
    }
}

#exit;

#
# Rotate through previous sync logs
#
my $logbase  = "/data/hosts/$host/upgsync-logs/log";
my $logcount = 5;
unlink( $logbase . "." . $logcount );
for my $i ( reverse( 1 .. $logcount - 1 ) ) {
    if ( -e $logbase . "." . $i ) {
        my $nextlog = $logbase . "." . ( $i + 1 );
        rename( $logbase . "." . $i, $nextlog );
    }
}
rename( $logbase, $logbase . ".1" );

open( STDOUT, "|tee $logbase" );
open( STDERR, ">&STDOUT" );

my @dirs = (
    { path => "/etc" },
    { path => "/home", exclude => ["local/messages.debug"] },
    { path => "/local", exclude => [ "logs/access_log.*", "logs/error_log.*" ] },
    { path => "/usr/share/authsrv" },
    { path => "/data" },
    { path => "/var", exclude => [ "lib", "cache", "log/journal" ] },
    { path => "/var/log", exclude => ["journal"] },
    { path => "/var/spool/cron" },
    { path => "/root",    exclude => [".ccache"] },
    { path => "/ora" },
);

my @partial = ("-W");
if ($partial) {
    @partial = ();
}

#
# Run the sync
#
foreach my $dref (@dirs) {
    my $dir = $dref->{path};

    my $tname = $dir;
    $tname =~ s|^/||go;
    $tname =~ s|/$||go;
    $tname =~ s|/|-|go;

    open( my $in, "-|" ) || exec("ssh -l root $host ls -al $dir 2>&1");
    my $out = join( "", <$in> );
    close($in);

    print "\n";

    if ( $out =~ /No such file or directory/m ) {
        print "-" x 70, "\n";
        print "Skipping $host:$dir - does not exist on source.\n";
        print "-" x 70, "\n";
        next;
    }

    print "-" x 70, "\n";
    print "Syncing $host:$dir\n";
    print "-" x 70, "\n";

    my @cmd = ( "rsync", "-avH", @partial, "--force", "--delete", "--progress" );

    if ( $dref->{exclude} ) {
        push( @cmd, "--delete-excluded" );
        foreach my $pat ( @{ $dref->{exclude} } ) {
            push( @cmd, "--exclude" => $pat );
        }
    }

    push( @cmd, "root\@$host:$dir/" => "$base/$tname/" );
    print "+ ", join( " ", @cmd ), "\n";
    system(@cmd);
}

my %cmds = (
    "uname -a"          => "uname-a",
    "df -k"             => "df-k",
    "df -i"             => "df-i",
    "vgdisplay -v"      => "vgdisplay",
    "lvs"               => "lvs",
    "cat /proc/cpuinfo" => "cpuinfo",
    "cat /proc/meminfo" => "meminfo",
    "ifconfig -a"       => "ifconfig",
    "ps -ef"            => "ps-ef",
    "ps auxwww"         => "ps-auxwww",
);

foreach my $cmd ( sort( keys(%cmds) ) ) {
    my $fname = $cmds{$cmd};

    system("ssh root\@$host $cmd > $base/info-$fname.txt");
}